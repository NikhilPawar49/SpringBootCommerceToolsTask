plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id 'org.openapi.generator' version "${openApiGeneratorVersion}"
}

group = 'com.commercetools'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion.toInteger())
    }
}

repositories {
    mavenCentral()
}

dependencies {

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocOpenApiVersion}"

    implementation "org.openapitools:jackson-databind-nullable:${jacksonNullableVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation "io.github.openfeign.form:feign-form-spring:${feignFormVersion}"
    implementation "io.github.openfeign:feign-httpclient:${feignHttpClientVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"

    implementation "com.commercetools.sdk:commercetools-sdk-java-api:17.6.0"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/openapi.yaml"
    outputDir = "$buildDir/generated"

    apiPackage = "com.example.api.controller"
    modelPackage = "com.example.api.model"

    configOptions = [
            useSpringBoot3 : "true",
            dateLibrary    : "java8",
            useTags        : "true",
            useLombok      : "true",
            lombokData     : "true"
    ]
}



sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

tasks.compileJava {
    dependsOn tasks.openApiGenerate
}

springBoot {
    mainClass = 'com.example.commercetoolsDemo.CommercetoolsDemoApplication'
}
